plugins {
    id 'com.android.library'
}

android {
    compileSdk 30

    defaultConfig {
        minSdk 26
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        def documentProviderAuthority = "com.marcantony.smbprovider.documents"
        manifestPlaceholders = [documentProviderAuthority: documentProviderAuthority]
        buildConfigField("String", "DOCUMENT_PROVIDER_AUTHORITY", "\"${documentProviderAuthority}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation project(':domain')
    implementation project(':persistence')

    implementation "io.reactivex.rxjava3:rxjava:${rootProject.ext.rxjavaVersion}"

    implementation 'com.hierynomus:smbj:0.10.0'
    implementation 'eu.agno3.jcifs:jcifs-ng:2.1.5'
    implementation('org.bouncycastle:bcprov-jdk15on') {
        version {
            strictly '1.65'
        }
    }

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}